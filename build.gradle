version '0.1.1'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'docker'


sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'ch.newsriver.beamer.BeamerMain'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources {

    filesMatching('**/version.properties') {expand projectVersion: project.version}

}

dependencies {
    compile project(':Newsriver-lib')
    compile 'org.elasticsearch:elasticsearch:2.3.1'
    compile 'org.eclipse.jetty:jetty-server:9.3.8.v20160314'
    compile 'org.eclipse.jetty:jetty-servlet:9.3.8.v20160314'
    compile 'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.3.8.v20160314'
    compile 'javax.websocket:javax.websocket-api:1.1'

    compile 'org.glassfish.jersey.containers:jersey-container-jetty-http:2.22.2'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.22.2'
    compile 'org.glassfish.jersey.core:jersey-server:2.22.2'

    compile 'com.mandrillapp.wrapper.lutung:lutung:0.0.7'


    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.2'
    compile 'org.apache.kafka:kafka-clients:0.10.0.0'
}



task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Newsriver Beamer',
                'Implementation-Version': version,
                'Main-Class': 'ch.newsriver.beamer.BeamerMain'
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    zip64 true
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}


docker {
    registry = 'https://docker-registry.newsriver.io:5000'

}

task buildimage(type: Docker, dependsOn:[clean,fatJar]) {
    push = true
    tagVersion = 'latest'
    registry= 'docker-registry.newsriver.io:5000'
    applicationName = 'newsriver-beamer'
    dockerfile = project.file('Dockerfile')

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}